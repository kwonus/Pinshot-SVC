// AVX-Quelle Grammar - Expressed in PEG
// Copyright (c) 2023, Kevin Wonus
// MIT License

// Short summary:
// command has multiple clauses and optional export/scope/macro support
// each clause is a standalone unit.  Search-Clauses have more granular units/syntax
// search-segment contains zero or one expression and/or any number of assignments
// expression contains [>= 1] fragments
// fragments contain [>= 1] features
// fragments are used to match terms (i.e. word tokens) across the corpus being searched; match logic should consider all features of the fragment/token when deeming it to be matched
//

WHITESPACE = _ { " " | "\t" | "\r" | "\n" }
statement = { avx_rev | singleton | implicits }
implicits = { segment ~ additional_segment* }
AVX_QUERY = _{ ^"_AVX_REV_" ~  "=?" }
avx_rev = { AVX_QUERY ~ "2.4.111" }   // grammar rule allows clients to assert expected version compatibility via extern C function call

segment = { elements ~ macro_suffix? }
elements = { element* ~ expression? ~ element* }
macro_suffix = _{ apply_macro_full | apply_macro_part }
expression = { search | invoke_full }
element = { var_opt | invoke_partial | filter }
singleton = { exit | help | history_reset | history | review | delete | absorb | get | set | clear | print }

export = { (output | append | overwrite) }
additional_segment = _{ SEARCH_EXPLICIT_BOUNDARY ~ segment }
filter = { FILTER_OP ~ (bookName | bookNum) ~ (verse_range | chapter_range | verse | chapter)? }

item = { (wildcard | text | pos | pn_pos12 | pos32 | nupos | greek | hebrew | loc | seg | lemma | punc | decoration | delta ) }
fragment = { feature_option ~ (AND ~ feature_option)* }
feature_option = { feature ~ (OR ~ feature)* }
feature = { not? ~ item}

delta = { ^"/delta/" }

pn = { ^"/1p/" | ^"/2p/" | ^"/3p/" | "/indefinite/" }
noun = { ^"/noun/" | ^"/n/" }
verb = { ^"/verb/" | ^"/v/" }
adj = { ^"/adjective/" | ^"/adj/" }
adv = { ^"/adverb/" | ^"/adv/" }
det = { ^"/determiner/" | ^"/det/" }
art = { ^"/particle/" | ^"/part/" }

most = { noun | verb | adj | adv | det | art }

pro = { ^"/pronoun/" | ^"/pn/" }

pro_neuter = { ^"/pn_neuter/" }
pro_masculine = { ^"/pn_masculine/" | ^"/pn_male/" }
pro_feminine = { ^"/pn_feminine/" | ^"/pn_fem/" }

pro_genitive   = { ^"/pn_genitive/"   | ^"/pn_gen/" }
pro_nominative = { ^"/pn_nominative/" | ^"/pn_nom/" }
pro_objective  = { ^"/pn_objective/"  | ^"/pn_obj/" }
pro_reflexive  = { ^"/pn_reflexive/"  | ^"/pn_rfx/" }

pro_all = { pro | pro_neuter| pro_masculine | pro_feminine | pro_genitive | pro_nominative | pro_objective | pro_reflexive }

prep = { ^"/preposition/" | ^"/prep/" }
inter = { ^"/interjection/" | ^"/inter/" }
conj = { ^"/conjunction/" | ^"/conj/" }
num = { ^"/numeric/" | ^"/num/" }

more = { prep | inter | conj | num }

wh = { ^"/wh/" }
singular = { ^"/singular/" }
plural = { ^"/pural/" }
pos = { most | more | pro_all | wh | singular | plural | pn }

loc_bob = { ^"/BoB/" }
loc_eob = { ^"/EoB/" }
loc_boc = { ^"/BoC/" }
loc_eoc = { ^"/EoC/" }
loc_bov = { ^"/BoV/" }
loc_eov = { ^"/EoV/" }
loc = { loc_bob | loc_eob | loc_boc | loc_eoc | loc_bov | loc_eov }

seg_hsm = { ^"/Hsm/"  }
seg_csm = { ^"/Csm/"  }
seg_ssm = { ^"/Ssm/"  }
seg_any = { ^"/sm/"   }
seg = { seg_hsm | seg_csm | seg_ssm | seg_any }

punc_any      = { "/_/" }
punc_exclaim  = { "/!/" }
punc_question = { "/?/" }
punc_declare  = { "/./" }
punc_dash     = { "/-/" }
punc_semi     = { "/;/" }
punc_comma    = { "/,/" }
punc_colon    = { "/:/" }
punc_possessive = { "/\'/" }
punc_parenthetical = { "/(/" }
punc_parenthetical_close = { "/)/" }
punc = { punc_any | punc_question | punc_declare | punc_dash | punc_semi | punc_comma | punc_colon | punc_possessive | punc_parenthetical | punc_parenthetical_close }

italics = { ^"/italics/" }
jesus = { ^"/Jesus/" }

print = { SINGLETON ~ ^"print" ~ export? }
exit = { SINGLETON ~ ^"exit" }
help = { SINGLETON ~ ^"help" ~ topic? }

topic = ${ SINGLETON? ~ (root_topics | group_topics) }
review = { SINGLETON ~ ^"review" ~ (date_range? ~ label_parameter* ~ date_range?) }

history_reset = { SINGLETON ~ ^"history" ~ "-reset" ~  range? }
history = { SINGLETON ~ ^"history" ~ (date_range? ~ historic_parameter* ~ date_range?) }
delete = { SINGLETON ~ ^"delete" ~ (label_any | historic_any) }
absorb = { SINGLETON ~ ^"absorb" ~ (label_any | historic_any) }
get = { SINGLETON ~ ^"get" ~ get_opt? }
clear = { SINGLETON ~ ^"clear" ~ (var | ALL) }
var = { span_key | lexicon_key | display_key | match_key | format_key }
set = { span_set | lexicon_set | display_set | match_set | format_set }

search = { ordered | unordered }   // quoted-searches are ordered // unquoted-searches are unordered
unordered = { fragment+ } // Unordered expression
ordered = { QUOTE ~ anchored ~ (anchored ~ unanchored)* ~ QUOTE } // Quoted/Ordered expression
unanchored = { ellipsis ~ fragment }
anchored = { fragment } // Segments have one or more unquoted (unordered) fragments <xor> have one or more quoted (ordered) fragments // Fragments are anded .... ALL MUST MATCH

span_key = @{ ^"span" }
SPAN_VAR = _{ span_key }
span_set = { SET ~ SPAN_VAR ~ ASSIGN ~ span_option }
span_var = { VAR ~ SPAN_VAR ~ ASSIGN ~ span_option }
span_get = { VAR ~ SPAN_VAR }
span_option = { DIGIT_le3 | span_verse }
span_verse = ${ ^"verse" }

lexicon_key = @{ ^"lexicon" }
LEXICON_VAR = _{ lexicon_key }
lexicon_set = { SET ~ LEXICON_VAR ~ ASSIGN ~ lexicon_option }
lexicon_var = { VAR ~ LEXICON_VAR ~ ASSIGN ~ lexicon_option }
lexicon_get = { VAR ~ LEXICON_VAR }
lexicon_option = { LEXICONS }
LEXICONS = _{ avx | av | dual }  // very specific domain for the AVX driver/parser implementation
avx = { ^"modern" | "^avx" }
av = { ^"kjv" | ^"av" }
dual = { ^"dual" | ^"both" }

display_key = @{ ^"display" }
DISPLAY_VAR = _{ display_key }
display_set = { SET ~ DISPLAY_VAR ~ ASSIGN ~ display_option }
display_var = { VAR ~ DISPLAY_VAR ~ ASSIGN ~ display_option }
display_get = { VAR ~ DISPLAY_VAR }
display_option = { DISPLAYS }
DISPLAYS = _{ av | avx }  // very specific domain for the AVX driver/parser implementation

match_key = @{ ^"similarity" }
MATCH_VAR = _{ match_key }
match_set = { SET ~ MATCH_VAR ~ ASSIGN ~ match_option }
match_var = { VAR ~ MATCH_VAR ~ ASSIGN ~ match_option }
match_get = { VAR ~ MATCH_VAR }
match_option =${ EXACT | ((PRECISE | FUZZY) ~ enable_lemma_match) }
PRECISE = ${ ^"precise" ~ "100" }
EXACT = ${ ^"exact" ~ "0" }
THIRTY_SOMETHING = ${ "3" ~ ('3' .. '9') }
FORTY_PLUS = ${ ('4' .. '9') ~ ('0' .. '9') }

FUZZY = ${ THIRTY_SOMETHING | FORTY_PLUS } // legal thresholds are from 33 to 99 (100% is excluded because that would represent an exact-match

format_key = @{ ^"format" }
FORMAT_VAR = _{ format_key }
format_set = { SET ~ FORMAT_VAR ~ ASSIGN ~ format_option }
format_var = { VAR ~ FORMAT_VAR ~ ASSIGN ~ format_option }
format_get = { VAR ~ FORMAT_VAR }
format_option = { html | markdown | textual | json | yaml }
html = ${ ^"html" }
markdown = ${ ^"markdown" | ^"md" }
textual = ${ ^"text" | ^"utf8" }
json = ${ ^"json" }
yaml = ${ ^"yaml" }

book = { (bookName | bookNum) }
verse = ${ chapter ~ COLON ~ DIGIT_byte }
verse_range = ${ COLON ~ DIGIT_byte ~ HYPHEN ~ DIGIT_byte}
chapter = ${ COLON ~ DIGIT_byte }
chapter_range = ${ COLON ~ DIGIT_byte ~ HYPHEN ~ DIGIT_byte}
bookName = ${ ALPHA+ | (onetwothree ~ ALPHA+) }
onetwothree = { ('1' .. '3') }
bookNum  = ${ ('1' .. '9') | (('1' .. '5') ~ DIGIT) ~ ( "6" ~ ('1' .. '6')) } // 1-66
var_opt = { span_var | lexicon_var | display_var | match_var | format_var }
get_opt = { span_get | lexicon_get | display_get | match_get | format_get | version_get | all_get }
apply_macro_part = { "||" ~ label_part} // macro is a Rust keyword and cannot be used in here as  PEG variable
apply_macro_full =  { "||" ~ label_full} // macro is a Rust keyword and cannot be used in here as PEG variable

invoke_full = ${ label_full | historic_full }
invoke_partial = ${ label_part | historic_part | defaults | currents }
label_parameter = _{ label_any | wildcard }
label_any = ${ (POUND | DOLLAR) ~ label }
label_full = ${ DOLLAR ~ label }
label_part = ${ POUND ~ label }
historic_parameter = _{ historic_sequence | historic_any | range }
historic_sequence = ${ id ~ HYPHEN ~ id }
historic_any = ${ (POUND | DOLLAR) ~ id }
historic_full = ${ DOLLAR ~ id }
historic_part = ${ POUND ~ id }
id = ${ ('1' .. '9') ~ ( '0' .. '9')* }
defaults = ${ POUND ~ "0" }
currents = ${ POUND ~ "*" }
current = ${ DOLLAR ~ "*" }

decoration = { italics | jesus }
pos32 = ${ "#" ~ HEX32 }
pn_pos12 = ${ "#" ~ HEX16 }
greek = ${ DIGITS ~ ^":G" }
hebrew = ${ DIGITS ~ ^":H" }

lemma = { "\\" ~ text ~ "\\" }

wildcard = ${ (term_begin | STAR) ~ term_contains* ~ term_end? }

ellipsis = _{ "..." }

append = { ">>" ~  filename }
output = { ">" ~ filename }
overwrite = { "=>" ~ filename }

FILESPEC = _{ ('a' .. 'z' | 'A' .. 'Z' | DIGIT) | "/" | "\\" | "-" | "_" | ":" }
quoted_filespec = ${ QUOTE ~ FILESPEC ~ (FILESPEC | " ") + ~ QUOTE }
unquoted_legal = ${ FILESPEC+ }
filename = { quoted_filespec | unquoted_legal }

// SEARCH
TOPIC_FIND = @{ ^"FIND" } // 1
TOPIC_FILTER = @{ ^"FILTER" } // 2
// CONTROL
TOPIC_SET = @{ ^"SET" } // 3
TOPIC_CLEAR = @{ ^"CLEAR" } // 4
TOPIC_OPTION = @{ ^"OPTION" } // NEW
TOPIC_GET = @{ ^"GET" } // 5
TOPIC_RESET = @{ ^"RESET" } // 6
TOPIC_ABSORB = @{ ^"ABSORB" } // 7
//OUTPUT
TOPIC_PRINT = @{ ^"PRINT" } // 8
TOPIC_EXPORT = @{ ^"EXPORT" } // 9
// SYSTEM
TOPIC_HELP = @{ ^"HELP" } // 10
TOPIC_VERSION = @{ ^"VERSION" } // 11
TOPIC_EXIT = @{ ^"EXIT" } // 12
// HISTORY & LABELING
TOPIC_INVOKE = @{ ^"INVOKE" } // 13
TOPIC_APPLY = @{ ^"APPLY" }   // 14
TOPIC_DELETE = @{ ^"DELETE" } // 10
TOPIC_REVIEW = @{ ^"REVIEW" } // 16
TOPIC_INIT = @{ ^"INITIALIZE" } // 17 **
root_topics = { TOPIC_FIND | TOPIC_FILTER | TOPIC_SET | TOPIC_CLEAR | TOPIC_OPTION | TOPIC_GET | TOPIC_RESET | TOPIC_ABSORB | TOPIC_PRINT | TOPIC_EXPORT | TOPIC_HELP | TOPIC_VERSION | TOPIC_EXIT | TOPIC_INVOKE | TOPIC_APPLY | TOPIC_DELETE | TOPIC_REVIEW | TOPIC_INIT }

GROUP_TOPIC_SEARCH = @{ ^"SEARCH" }
GROUP_TOPIC_CONTROL = @{ ^"CONTROL" | ^"SETTINGS" }
GROUP_TOPIC_OUTPUT = @{ ^"OUTPUT" }
GROUP_TOPIC_SYSTEM = @{ ^"SYSTEM" }
GROUP_TOPIC_HISTORY_AND_LABELING = @{ ^"HISTORY" | ^"MACRO" | ^"LABEL" | ^"LABELING" | ^"LABELLING" }
group_topics = { GROUP_TOPIC_SYSTEM | GROUP_TOPIC_OUTPUT | GROUP_TOPIC_CONTROL | GROUP_TOPIC_SEARCH | GROUP_TOPIC_HISTORY_AND_LABELING }

nupos = {
	^"#a-acp" |
	^"#av" |
	^"#av-an" |
	^"#av-c" |
	^"#avc-jn" |
	^"#av-d" |
	^"#av-dc" |
	^"#av-ds" |
	^"#av-dx" |
	^"#av-j" |
	^"#av-jc" |
	^"#av-jn" |
	^"#av-js" |
	^"#av-n1" |
	^"#av-s" |
	^"#avs-jn" |
	^"#av-vvg" |
	^"#av-vvn" |
	^"#av-x" |
	^"#c-acp" |
	^"#cc" |
	^"#cc-acp" |
	^"#c-crq" |
	^"#ccx" |
	^"#crd" |
	^"#cs" |
	^"#cst" |
	^"#d" |
	^"#dc" |
	^"#dg" |
	^"#ds" |
	^"#dt" |
	^"#dx" |
	^"#fw-es" |
	^"#fw-fr" |
	^"#fw-ge" |
	^"#fw-gr" |
	^"#fw-it" |
	^"#fw-la" |
	^"#fw-mi" |
	^"#j" |
	^"#j-av" |
	^"#jc" |
	^"#jc-jn" |
	^"#jc-vvg" |
	^"#jc-vvn" |
	^"#j-jn" |
	^"#j-u" |
	^"#j-vvnu" |
	^"#jp" |
	^"#js" |
	^"#js-jn" |
	^"#js-vvg" |
	^"#js-vvn" |
	^"#j-vvg" |
	^"#j-vvn" |
	^"#n1" |
	^"#n1-an" |
	^"#n1-j" |
	^"#n11vbz" |
	^"#n2" |
	^"#n2-acp" |
	^"#n2-an" |
	^"#n2-av" |
	^"#n2-dx" |
	^"#n2-j" |
	^"#n2-jn" |
	^"#n2-vdg" |
	^"#n2-vhg" |
	^"#n2-vvg" |
	^"#n2-vvn" |
	^"#ng1" |
	^"#ng1-an" |
	^"#ng1-j" |
	^"#ng1-jn" |
	^"#ng1-vvn" |
	^"#ng2" |
	^"#ng2-jn" |
	^"#n-jn" |
	^"#njp" |
	^"#njp2" |
	^"#njpg1" |
	^"#njpg2" |
	^"#np1" |
	^"#np2" |
	^"#npg1" |
	^"#npg2" |
	^"#np-n1" |
	^"#np-n2" |
	^"#np-ng1" |
	^"#np1-n" |
	^"#n-vdg" |
	^"#n-vhg" |
	^"#n-vvg" |
	^"#n-vvn" |
	^"#ord" |
	^"#p-acp" |
	^"#pc-acp" |
	^"#pi" |
	^"#pi2" |
	^"#pi2x" |
	^"#pig" |
	^"#pigx" |
	^"#pix" |
	^"#pn22" |
	^"#pn31" |
	^"#png11" |
	^"#png12" |
	^"#png21" |
	^"#png22" |
	^"#png31" |
	^"#png32" |
	^"#pno11" |
	^"#pno12" |
	^"#pno21" |
	^"#pno31" |
	^"#pno32" |
	^"#pns11" |
	^"#pns12" |
	^"#pns21" |
	^"#pns31" |
	^"#pns32" |
	^"#po11" |
	^"#po12" |
	^"#po21" |
	^"#po22" |
	^"#po31" |
	^"#po32" |
	^"#pp" |
	^"#pp-f" |
	^"#px11" |
	^"#px12" |
	^"#px21" |
	^"#px22" |
	^"#px31" |
	^"#px32" |
	^"#pxg21" |
	^"#q-crq" |
	^"#r-crq" |
	^"#ro-crq" |
	^"#qo-crq" |
	^"#rg-crq" |
	^"#n-crq" |
	^"#sy" |
	^"#uh" |
	^"#uh-av" |
	^"#uh-crq" |
	^"#uh-dx" |
	^"#uh-j" |
	^"#uh-jn" |
	^"#uh-np" |
	^"#uh-n" |
	^"#uh-v" |
	^"#uh-x" |
	^"#vb2" |
	^"#vb2-imp" |
	^"#vb2r" |
	^"#vb2x" |
	^"#vbb" |
	^"#vbbx" |
	^"#vbd" |
	^"#vbds" |
	^"#vbd2s" |
	^"#vbdr" |
	^"#vbd2r" |
	^"#vbd2" |
	^"#vbd2x" |
	^"#vbdp" |
	^"#vbdx" |
	^"#vbg" |
	^"#vbi" |
	^"#vbm" |
	^"#vbmx" |
	^"#vbn" |
	^"#vbp" |
	^"#vbr" |
	^"#vbz" |
	^"#vbzx" |
	^"#vd2" |
	^"#vd2-imp" |
	^"#vd2x" |
	^"#vdb" |
	^"#vdbx" |
	^"#vdd" |
	^"#vdd2" |
	^"#vdd2x" |
	^"#vddp" |
	^"#vddx" |
	^"#vdg" |
	^"#vdi" |
	^"#vdn" |
	^"#vdp" |
	^"#vdz" |
	^"#vdzx" |
	^"#vh2" |
	^"#vh2-imp" |
	^"#vh2x" |
	^"#vhb" |
	^"#vhbx" |
	^"#vhd" |
	^"#vhd2" |
	^"#vhdp" |
	^"#vhdx" |
	^"#vhg" |
	^"#vhi" |
	^"#vhn" |
	^"#vhp" |
	^"#vhz" |
	^"#vhzx" |
	^"#vm2" |
	^"#vm2x" |
	^"#vmb" |
	^"#vmb1" |
	^"#vmbx" |
	^"#vmd" |
	^"#vm2" |
	^"#vmd2" |
	^"#vmd2x" |
	^"#vmdp" |
	^"#vmdx" |
	^"#vmi" |
	^"#vmn" |
	^"#vmp" |
	^"#vv2" |
	^"#vv2-imp" |
	^"#vv2x" |
	^"#vvb" |
	^"#vvbx" |
	^"#vvd" |
	^"#vvd2" |
	^"#vvd2x" |
	^"#vvdp" |
	^"#vvdx" |
	^"#vvg" |
	^"#vvi" |
	^"#vvn" |
	^"#vvp" |
	^"#vvz" |
	^"#vvzx" |
	^"#xx" |
	^"#zz" }

enable_lemma_match = { "!" }

range = _{ date_range | historic_sequence }
date_range = _{ (date_gt ~ date_lt?) | (date_lt ~ date_gt?) }
historic_range = _{ (id_gt ~ id_lt?) | (id_lt ~ id_gt?) }
history_max = { DIGITS }
history_date = ${ "20" ~ DIGIT ~ DIGIT ~ "/" ~ DIGIT ~ DIGIT? ~ "/" ~ DIGIT ~ DIGIT? }
date_gt = { ">" ~ history_date }
date_lt = { "<" ~ history_date }
id_gt = { ">" ~ id }
id_lt = { "<" ~ id }
reset_less_than = _{ id_lt | date_lt }
reset_greater_than = _{ id_gt | date_gt }

DIGIT = _{ '0' .. '9' }
HEX = _{ DIGIT | ('a' .. 'f') | ('A' .. 'F') }
DIGITS = ${ DIGIT+ }
DIGIT_ge5 = ${DIGIT ~ DIGIT ~ DIGIT ~ DIGIT ~ DIGIT+ }
DIGIT_le2 = ${DIGIT ~ DIGIT? }
DIGIT_le3 = ${DIGIT ~ DIGIT ~ DIGIT? }
DIGIT_byte = ${ (("0" | "1" | "2") ~ DIGIT ~ DIGIT) | (DIGIT ~ DIGIT?) }
HEX16 = ${ HEX ~ HEX ~ HEX ~ HEX }
HEX32 = ${ HEX ~ HEX ~ HEX ~ HEX ~ HEX ~ HEX ~ HEX ~ HEX }
AND = _{ "&" }

label = ${ LABEL_CHAR* }
LABEL_CHAR = _{ ( 'a' .. 'z' | 'A' .. 'Z' | DIGIT | "-" | "_") }

ALL = _{ ^"all" }
all_get = { VAR ~ ALL }
VERSION = _{ ^"version" }
version_get = { VAR ~ VERSION }
OR = _{ "|" }
SEARCH_EXPLICIT_BOUNDARY = _{ "+" }
OPEN_BRACE = _{ "[" }
CLOSE_BRACE = _{ "]" }
DOLLAR = _{ "$" }
POUND = _{ "#" }
FILTER_OP = _{ "<" }
QUOTE = _{ "\"" }
COLON = _{ ":" }
ASSIGN = _{ "=" }
STAR = _{ "*" }
SPACE = _{ " " }
VAR = _{ "%" }
SET = _{ SINGLETON ~ set_command? ~ VAR }
set_command = ${ ^"set" }
SINGLETON = _{ "@" }
not = { "-" }

term_contains = $ { text ~ STAR }
term_end = ${ text }
term_begin = ${ text ~ STAR }
text = ${ ALPHA+ }
ALPHA = _{ 'a' .. 'z' | 'A' .. 'Z' }


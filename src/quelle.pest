// Vanilla-Quelle Grammar - Expressed in PEG
// Copyright (c) 2023, Kevin Wonus
// MIT License

// Short summary:
// command has multiple clauses and optional export/scope/macro support
// each clause is a standalone unit.  Search-Clauses have more granular units/syntax
// search-clause contains [>= 1] segments
// segments contain [>= 1] fragments
// fragments contain [>= 1] features
// fragments are used to match terms (i.e. word tokens) across the corpus being searched; match logic should consider all features of the fragment/token when deeming it to be matched

WHITESPACE = _ { " " | "\t" | "\r" | "\n" }
space = _ { " " | "\t" }
statement = { (clauses ~ save) | (clauses ~ scope ~ export) | (clauses ~ export ~ scope) | (clauses ~ export) | (clauses ~ scope) | clauses | explicit }
scope = { pick | filter }
pick = { "<<[" ~ digit+ ~ ("," ~ digit+)* | "]" }
cv = ${ chapter ~ ":" ~ verse }
bcv = { (bookName | bookNum)}
chapter = ${ digit+ }
verse = ${ digit+ }
bookName = ${ alpha+ | (onetwothree ~ space* ~ alpha+) }
onetwothree = { ('1' .. '3') }
bookNum  = ${ ('1' .. '9') | (('1' .. '5') ~ digit) ~ ( "6" ~ ('1' .. '6')) } // 1-66
filter = ${ positive? ~ space* ~ "\\" ~ ((bookName ~ space+) | bookNum ~ ":") ~ "\\" }
clauses = { clause ~ (polarity ~ clause)* }
clause = { search | setting | named | previous }
polarity = ${ negative | positive }
positive = ${ "+" | ";" }   // extra positive delimiters are permitted in front of save, filter, output, and negative. This will allow user to always terminate clauses with semicolon [or plus]
negative = ${ positive? ~ space+ ~ "--" ~ space* }
setting = { span | domain | exact /* | heading | annotation */ }
label = { ('a' .. 'z' | 'A' .. 'Z' | '0' .. '9' | " " | "-" | "_")+ }
named = ${ space* ~ "{" ~ space* ~ label ~ space* ~ "}" ~ space*}   // macro is a Rust keyword; cannot use it here
save = ${ positive? ~ space* ~ "<<" ~ space* ~ label}
previous = { "{" ~ ('0' .. '9')+ ~ "}" }
digit = _ { '0' .. '9' }
alpha = _ { 'a' .. 'z' | 'A' .. 'Z' }
text = { alpha+ | "-" }
loc = { ^"BoB" | ^"EoB" | ^"BoC" | ^"EoC" | ^"BoV" | ^"EoV" }
and = { "&" }
or = { "|" }

search = { ordered | segment+ }   // New nomenclature; searches have = 1 ordered-segments <xor> have >= 1 unordered-segments; segments are separated by OR ( | )

segment = { fragment ~ (or ~ fragment)* } // Segments have >= 1 Unquoted (unordered) fragments <xor> have >= 1quoted/bracketed (unordered) fragments
fragment = { feature ~ (and ~ feature)* } // Fragments have >= 1 features, separated by AND( & ) // A single token in a document must match all features to be deemed a full-match // partial-matches are not designed into the grammar

ordered = { "\"" ~ (anchored ~ (unanchored | anchored)*) ~ "\"" } // Quoted/Ordered segment
anchored = _{ (unordered | (fragment ~ (or ~ fragment)*)) }
unanchored = { ellipsis ~ anchored }
unordered = { "[" ~ segment ~ segment* ~ "]" }

feature = ${ space* ~ (term | pos | pn | lemma | wildcard | punc) ~ space* }
term = { text+ }
pn = { "#" ~ ('0' .. '3') }
pos = { "/" ~ alpha+ ~ "/" }
lemma = { "#" ~ alpha+ }
wildcard = { ("*" ~ alpha+) | (alpha+ ~ "*" ) | "*" }
punc = { "/[.,:;!?]/" }
ellipsis = _{ "..." }

explicit = { exit | help | history | expand | delete | version | get }
exit = { ^"@exit" }
help = { ^"@help" ~ topic | ^"@help" }
history = {^"@history" }
topic = ${ space* ~ alpha+ ~ space* }
expand = { ^"@expand" ~ named* }
delete = { ^"@delete" ~  named* }
version = { ^"@version" }
get = { ^"@get" ~ var }
var = ${ space* ~ (spanKey | domainKey | exactKey | searchAll) ~space*}
export = { positive? ~ (output | overwrite | append) }
overwrite = ${ output ~ "!" ~space* }
append = ${ ">>" ~  space* ~ filename ~space* }
output = ${ ">" ~ space* ~ filename ~space* }

legal = _ { ('a' .. 'z' | 'A' .. 'Z' | '0' .. '9') | "/" | "<" | "-" | "_" }
filename = ${ space* ~ (("\"" ~ (legal+ | " ") + ~ "\"" ) | legal+) ~ space* }

span = { spanSet | spanDel }
spanEq = { spanKey ~ "=" }
spanDel = { spanEq ~ "@" }
spanSet = { spanEq ~ digit+ }
SPAN = { ^"span" }
spanKey = { SPAN | (searchPrefix ~ SPAN) }
searchPrefix =  { searchAll ~ "." }
searchAll =  { ^"search" }

domain = { domainSet | domainDel }
domainEq = { domainKey ~ "=" }
domainDel = { domainEq ~ "@" }
domainSet = { domainEq ~ domains }
DOMAIN = { ^"domain" }
domainKey = { DOMAIN | (searchPrefix ~ DOMAIN) }
domains = { av | avx }  // very specific domain for the AVX driver/parser implementation
avx = { (("M"|"m")~("O"|"o")~("D"|"d")~("E"|"e")~("R"|"r")~("N"|"n")) | (("A"|"a") ~ ("V"|"v") ~ ("X"|"x")) }
av = { (("K"|"k") ~ ("J"|"j") ~ ("V"|"v")) | (("A"|"a") ~ ("V"|"v")) | (("B"|"b") ~ ("I"|"i") ~ ("B"|"b") ~ ("L"|"l") ~ ("E"|"e")) }

boolean = { (("T"|"t") ~ ("R"|"r") ~ ("U"|"u") ~ ("E"|"e")) | (("F"|"f") ~ ("A"|"a") ~ ("L"|"l") ~ ("S"|"s") ~ ("E"|"e")) | "0" | "1" }

exact = { exactSet | exactDel }
exactEq = { exactKey ~ "=" }
exactDel = { exactEq ~ "@" }
exactSet = { exactEq ~ boolean }
EXACT = { ^"exact"}
exactKey = { EXACT | (searchPrefix ~ EXACT) }
